plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.dorongold.task-tree' version '1.5'
}

group 'com.example'
version '1.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework:spring-core:5.2.+"
}

dependencyLocking {
    lockAllConfigurations()
}

publishing {

    publications {
        jarPackage(MavenPublication) {
            from components.java

            versionMapping {
                VersionMappingStrategy
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

tasks.register('resolveAndLockAll') {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.findAll {
            // Add any custom filtering on the configurations to be resolved
            it.canBeResolved
        }.each { it.resolve() }
    }
}